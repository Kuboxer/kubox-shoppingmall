plugins {
    id 'java'
    id 'org.springframework.boot' version '3.1.5'
    id 'io.spring.dependency-management' version '1.1.3'
}

group = 'com.shop'
version = '1.0.0'

java {
    sourceCompatibility = '17'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'

    
    runtimeOnly 'com.mysql:mysql-connector-j'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

tasks.named('test') {
    useJUnitPlatform()
}

// .env 파일 로드 및 bootRun 태스크 설정
bootRun {
    doFirst {
        def envFile = file('../.env')
        if (envFile.exists()) {
            envFile.readLines().each { line ->
                if (line && !line.startsWith('#') && line.contains('=')) {
                    def (key, value) = line.split('=', 2)
                    systemProperty key.trim(), value.trim()
                }
            }
        }
    }
}

